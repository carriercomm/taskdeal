#!/usr/bin/env perl

use strict;
use warnings;

use FindBin;
use lib "$FindBin::Bin/../extlib/lib/perl5";
use lib "$FindBin::Bin/../mojo/lib";
use lib "$FindBin::Bin/../lib";

use Config::Tiny;
use Taskdeal::Log;
use Taskdeal::Client;
use Mojo::UserAgent;
use Mojo::IOLoop;
use Sys::Hostname 'hostname';

# Log
my $log = Taskdeal::Log->new(path => "$FindBin::Bin/../log/taskdeal-client.log");

# Util
my $client = Taskdeal::Client->new(home => "$FindBin::Bin/..");
$client->log($log);

# Config
my $ct = Config::Tiny->new;
my $config_file = "$FindBin::Bin/../taskdeal-client.conf";
my $config = $ct->read($config_file);
die "Config read error $config_file: " . $ct->errstr if $ct->errstr;

# Config for development
my $config_my_file = "$FindBin::Bin/../taskdeal-client.my.conf";
if (-f $config_my_file) {
  my $config_my = $ct->read($config_my_file);
  die "Config read error: $config_my_file" . $ct->errstr if $ct->errstr;

  # Merge config
  for my $section (keys %$config_my) {
    $config->{$section}
      = {%{$config->{$section} || {}}, %{$config_my->{$section} || {}}};
  }
}

# User Agent
my $ua = Mojo::UserAgent->new;
$ua->inactivity_timeout(0);

# Server URL
my $server_host = $config->{server}{host} || 'localhost';
my $server_url = "ws://$server_host";
$ENV{TASKDEAL_SERVER_PORT} = 3000;
my $server_port = $ENV{TASKDEAL_SERVER_PORT} || $config->{server}{port} || '10040';
$server_url .= ":$server_port";

# Connect to server
my $websocket_cb;
$websocket_cb = sub {
  $ua->websocket($server_url => sub {
    my ($ua, $tx) = @_;
    
    # Web socket connection success
    if ($tx->is_websocket) {
      $log->info("Connect to $server_url.");
      
      # Send client information
      my $hostname = hostname;
      my $current_role = $client->current_role;
      my $name = $config->{client}{name};
      my $group = $config->{client}{group};
      my $description = $config->{client}{description};
      $tx->send({json => {
        type => 'client_info',
        current_role => $current_role,
        name => $name,
        group => $group,
        description => $description
      }});
      
      # Receive JSON message
      $tx->on(json => sub {
        my ($tx, $hash) = @_;
        
        my $type = $hash->{type} || '';
        if ($type eq 'sync' || $type eq 'task') {
          my $work_dir
            = $type eq 'sync' ? $FindBin::Bin : "$FindBin::Bin/task";
          
          if (chdir $work_dir) {
            my $command = $type eq 'sync' ? 'sync' : $hash->{command};
            my $args = $hash->{args} || [];
            
            if (system("./$command", @$args) == 0) {
              my $status = `echo $?`;
              if (($status || '') =~ /^0/) {
                my $message = "$type success. Command $command @$args";
                $log->info($message);
                $tx->send(json => {message => $message, success => 1});
              }
              else {
                my $message = "$type fail. Command $command @$args. Return bad status.";
                $log->error($message);
                $tx->send(json => {message => $message, success => 0});
              }
            } else {
              my $message = "$type fail. Command $command @$args. Command fail.";
              $log->error($message);
              $tx->send(json => {message => $message, success => 0});
            }
          }
          else {
            my $message = "$type fail. Can't change directory $FindBin::Bin: $!";
            $log->error($message);
            $tx->send(json => {message => $message, success => 0});
          }
        }
        else {
          my $message = "Unknown type $type";
          $log->error($message);
          $tx->send(json => {message => "Unknown type $type", success => 0});
        }
      });
      
      # Finish websocket connection
      $tx->on(finish => sub {
        $log->info("Connection is closed.");
        
        # Reconnect to server
        Mojo::IOLoop->timer(10 => sub { goto $websocket_cb });
      });
    }
    
    # Web socket connection fail
    else {
      $log->error("Can't connect to server: $server_url.");
      
      # Reconnect to server
      Mojo::IOLoop->timer(10 => sub { goto $websocket_cb });
    }
  });
};
$websocket_cb->();

Mojo::IOLoop->start unless Mojo::IOLoop->is_running;
